metadata:
    language: v1-beta
    name: "Header: X-Powered-By detection"
    description: "Identify the server software running on the application via 'X-Powered-By' header"
    tags: "passive","header","wstg"
    author: "cosasdepuma"

define:
    summary = "While exposed server information is not necessarily in itself a vulnerability, it is information that can assist attackers in exploiting other vulnerabilities that may exist. Exposed server information can also lead attackers to find version-specific server vulnerabilities that can be used to exploit unpatched servers."

given response then
    if {latest.response.headers} matches "[xX][_-][pP]owered[_-][bB]y:[^\n]+" then
        # -- | Software and version
        if {latest.response.headers} matches "[xX][_-][pP]owered[_-][bB]y:\s+[^/]+/[0-9]+" then
            report issue:
                severity: low
                confidence: certain
                detail: `The software used by the application, as well as its version, has been detected through the "X-Powered-By" header. {summary}`

        # -- | Software
        else then
            report issue:
                severity: info
                confidence: certain
                detail: `The software used by the application has been detected through the "X-Powered-By" header. {summary}`
        end if
    end if